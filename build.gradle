group "azadev.backt"
version "0.7"

buildscript {
	// https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-gradle-plugin
	// https://plugins.jetbrains.com/plugin/6954-kotlin
	ext.has("kotlin_version") || (ext.kotlin_version = "1.1.3-2")

	// https://github.com/Anizoptera/Kotlin-Logging-Facade
	ext.has("kotlog_version") || (ext.kotlog_version = "0.8.2")

	// http://mvnrepository.com/artifact/junit/junit
	ext.has("junit_version") || (ext.junit_version = "4.12")

	repositories {
		jcenter()
	}
	dependencies {
		//noinspection DifferentKotlinGradleVersion
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

		// https://bintray.com/jfrog/jfrog-jars/gradle-bintray-plugin
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
	}
}

apply plugin: "kotlin"

repositories {
	jcenter()
	maven { url "http://dl.bintray.com/azadev/maven" }
}

dependencies {
	//noinspection DifferentKotlinGradleVersion
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

	// https://github.com/Anizoptera/Kotlin-Logging-Facade
	compile "azadev.logging:kotlog:$kotlog_version"

	// http://mvnrepository.com/artifact/junit/junit
	testCompile "junit:junit:$junit_version"
}


// PUBLISHING
// https://docs.gradle.org/current/userguide/publishing_maven.html
// https://github.com/bintray/gradle-bintray-plugin

apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

def pub_artifactId = "backt_sql"

task jar_source(type: Jar) {
	from sourceSets.main.allSource
}

publishing {
	publications {
		"$pub_artifactId"(MavenPublication) {
			groupId group
			artifactId pub_artifactId
			version version

			from components.java

			artifact jar_source {
				classifier "sources"
			}
		}
	}
}

bintray {
	user = hasProperty("bintray_user") ? bintray_user : ""
	key = hasProperty("bintray_api_key") ? bintray_api_key : ""
	publications = [pub_artifactId]

	publish = true // [Default: false] Whether version should be auto published after an upload
	override = true // [Default: false] Whether to override version artifacts already published

	pkg {
		repo = "maven"
		name = pub_artifactId

		// Optional params used to create package if it doesn't exist
		userOrg = "azadev"
		licenses = ["MIT"]
		vcsUrl = "https://github.com/Anizoptera/BacKT_SQL.git"
		issueTrackerUrl = "https://github.com/Anizoptera/BacKT_SQL/issues"
		websiteUrl = "https://github.com/Anizoptera/BacKT_SQL"
		publicDownloadNumbers = false

		version {
			name = project.version
		}
	}
}
